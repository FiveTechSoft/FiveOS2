#include "FiveOS2.ch"

//----------------------------------------------------------------------------//

CLASS TSay FROM TControl

   METHOD New( nTop, nLeft, nWidth, nHeight, cPrompt, oWnd ) CONSTRUCTOR

   METHOD Display() VIRTUAL     // We request default painting

   METHOD ReDefine( nId, oDlg ) CONSTRUCTOR

   METHOD cToChar( nClassName, nControls, nDynData ) INLINE ;
          Super:cToChar( WC_STATIC, nControls, nDynData )

ENDCLASS

//----------------------------------------------------------------------------//

METHOD New( nTop, nLeft, nWidth, nHeight, cPrompt, oWnd ) CLASS TSay

   DEFAULT nWidth := 80, nHeight := 25, cPrompt := "~Label",;
           oWnd   := GetWndDefault()

   ::nTop     = nTop
   ::nLeft    = nLeft
   ::nBottom  = nTop + nHeight - 1
   ::nRight   = nLeft + nWidth - 1
   ::cCaption = cPrompt
   ::oWnd     = If( ! Empty( oWnd:oWndClient ), oWnd:oWndClient, oWnd )
   ::nStyle   = nOr( WS_VISIBLE, SS_TEXT )

   if ! Empty( oWnd:hWnd )
      ::hWnd = WinCreateWindow( ::oWnd:hWnd, WC_STATIC, cPrompt, ::nStyle,;
                                nTop, nLeft, nWidth, nHeight, ::oWnd:hWnd )
      ::Link()
      oWnd:AddControl( Self )
   else
      oWnd:DefControl( Self )
   endif

return nil

//----------------------------------------------------------------------------//

METHOD ReDefine( nId, oDlg ) CLASS TSay

   ::nId  = nId
   ::oWnd = oDlg

   ::oWnd:DefControl( Self )

return nil

//----------------------------------------------------------------------------//
